apply plugin: "idea"


subprojects {
    apply plugin: "java"

    repositories {
        mavenCentral()
        maven {
            url "https://repository.jboss.org/nexus/content/repositories/releases"
        }
        maven {
            url "http://repository.jboss.org/nexus/content/groups/public"
        }
    }

    sourceSets {
        integrationTest {
            java.srcDir file('src/integration-test/java')
            resources.srcDir file('src/integration-test/resources')
        }
    }

    task integrationTest(type: Test, dependsOn: jar) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
        systemProperties['jar.path'] = jar.archivePath
    }


    task integrationTestArchive(type: Jar, dependsOn: integrationTestClasses) {
        baseName = "itest-${project.archivesBaseName}"
        from sourceSets.test.output
        from sourceSets.integrationTest.output
    }

    task testArchive(type: Jar, dependsOn: testClasses) {
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }

    configurations {
        tests
        integrationTests
    }


    artifacts {
        tests testArchive
        integrationTests integrationTestArchive
    }


    task copyTestResources(type: Copy) {
        from "${project(':shared').projectDir}/src/test/resources/META-INF"
        from "${project(':shared').projectDir}/src/test/resources/webapp/WEB-INF"
        into "${project.buildDir}/classes/integrationTest"
    }

    dependencies {
        testCompile("junit:junit:$junitVersion")
        testCompile("org.mockito:mockito-all:$mockitoVersion")
        runtime("org.jboss.as:jboss-as-arquillian-container-managed:$jbossVersion")

        integrationTestCompile(
            sourceSets.main.output,
            configurations.testCompile,
            sourceSets.test.output,
            configurations.testRuntime
        )


        testCompile("org.jboss.arquillian.junit:arquillian-junit-container:$arquillianJunitVersion")
        integrationTestCompile("org.jboss.arquillian.junit:arquillian-junit-container:$arquillianJunitVersion")
    }

    check.dependsOn integrationTest
    processTestResources.dependsOn copyTestResources
}
